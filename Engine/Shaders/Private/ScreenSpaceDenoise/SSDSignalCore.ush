// Copyright 1998-2019 Epic Games, Inc. All Rights Reserved.

/*=============================================================================
	SSDCommon.ush: All the code commonly shared between uniform buffers.
=============================================================================*/

#pragma once

#include "SSDCommon.ush"


//------------------------------------------------------- STRUCTURE

/** Generic data structure to manipulate any kind of signal. */
struct FSSFSignalSample
{
	// Number of sample accumulated.
	float AccumulatedSampleCount;

	// Scene color and alpha.
	float4 SceneColor;

	// Shadow denoising specific.
	float LeavingRayCount;
	float PenumbraClosestHit;
	float UncertainRayCount;
};

FSSFSignalSample CreateSignalSampleFromScalarValue(float Scalar)
{
	FSSFSignalSample OutSample;
	OutSample.SceneColor = Scalar;
	OutSample.AccumulatedSampleCount = Scalar;
	OutSample.LeavingRayCount = Scalar;
	OutSample.PenumbraClosestHit = Scalar;
	OutSample.UncertainRayCount = Scalar;
	return OutSample;
}

FSSFSignalSample CreateSignalSample()
{
	return CreateSignalSampleFromScalarValue(0.0);
}


//------------------------------------------------------- INPUT/OUTPUT

/** Decode input signal sample. */
FSSFSignalSample DecodeInputSignal(const uint SignalBufferLayout, float4 RawSample[2])
{
	FSSFSignalSample OutSample = CreateSignalSample();

	if (SignalBufferLayout == SIGNAL_BUFFER_LAYOUT_PENUMBRA_INPUT)
	{
		OutSample.AccumulatedSampleCount = 1;
		OutSample.LeavingRayCount = OutSample.AccumulatedSampleCount * RawSample[0].r;
		OutSample.PenumbraClosestHit = RawSample[1].r;
	}
	else if (SignalBufferLayout == SIGNAL_BUFFER_LAYOUT_PENUMBRA_INTERNAL)
	{
		if (DEBUG_STORE_NORMALIZED_COMPONENTS)
		{
			OutSample.LeavingRayCount = RawSample[0].r * RawSample[0].b;
			OutSample.UncertainRayCount = RawSample[0].g * RawSample[0].b;
			OutSample.AccumulatedSampleCount = RawSample[0].b - OutSample.UncertainRayCount;
		}
		else
		{
			OutSample.LeavingRayCount = RawSample[0].r;
			OutSample.AccumulatedSampleCount = RawSample[0].g;
			OutSample.UncertainRayCount = RawSample[0].b;
		}
	}
	else if (SignalBufferLayout == SIGNAL_BUFFER_LAYOUT_PENUMBRA_HISTORY_REJECTION)
	{
		//if (Samples[0].r < 1)
		if (RawSample[0].r >= 0)
		{
			OutSample.AccumulatedSampleCount = 1;
			OutSample.LeavingRayCount = OutSample.AccumulatedSampleCount * RawSample[0].r; // *(255.0 / 254.0);
		}
	}
	else if (SignalBufferLayout == SIGNAL_BUFFER_LAYOUT_REFLECTIONS_INPUT)
	{
		OutSample.AccumulatedSampleCount = RawSample[1].r == -2 ? 0 : 1;
		OutSample.LeavingRayCount = OutSample.AccumulatedSampleCount * (1 - RawSample[0].a);
		OutSample.SceneColor = RawSample[0];

		// max() because ray hit distance is < 0 when miss / not ray has been shot.
		OutSample.PenumbraClosestHit = RawSample[1].r * OutSample.AccumulatedSampleCount;
		OutSample.PenumbraClosestHit = ((RawSample[1].r == -1.0) ? 1.0e20 : RawSample[1].r) * OutSample.AccumulatedSampleCount;
	}
	else if (SignalBufferLayout == SIGNAL_BUFFER_LAYOUT_REFLECTIONS_HISTORY)
	{
		OutSample.AccumulatedSampleCount = RawSample[1].r;
		OutSample.LeavingRayCount = OutSample.AccumulatedSampleCount * (1 - RawSample[0].a);
		OutSample.SceneColor = OutSample.AccumulatedSampleCount * RawSample[0];
	}
	else if (SignalBufferLayout == SIGNAL_BUFFER_LAYOUT_AO_INPUT)
	{
		OutSample.AccumulatedSampleCount = RawSample[0].g < 0 ? 0 : 1;
		OutSample.LeavingRayCount = OutSample.AccumulatedSampleCount * RawSample[0].r;
		OutSample.PenumbraClosestHit = OutSample.AccumulatedSampleCount * RawSample[0].g;
	}
	else if (SignalBufferLayout == SIGNAL_BUFFER_LAYOUT_AO_HISTORY)
	{
		OutSample.AccumulatedSampleCount = RawSample[0].g;
		OutSample.LeavingRayCount = OutSample.AccumulatedSampleCount * RawSample[0].r;
	}

	return OutSample;
}

/** Encode output signal sample. */
void EncodeOutputSignal(const uint SignalBufferLayout, FSSFSignalSample Sample, out float4 OutRawSample[2], out uint OutBufferCount)
{
	OutRawSample[0] = OutRawSample[1] = 0;
	OutBufferCount = 1;

	if (SignalBufferLayout == SIGNAL_BUFFER_LAYOUT_PENUMBRA_INTERNAL)
	{
		if (DEBUG_STORE_NORMALIZED_COMPONENTS)
		{
			OutRawSample[0] = float4(
				Sample.UncertainRayCount + Sample.AccumulatedSampleCount > 0
					? Sample.LeavingRayCount / (Sample.UncertainRayCount + Sample.AccumulatedSampleCount)
					: -1.0,
				Sample.UncertainRayCount + Sample.AccumulatedSampleCount > 0
					? Sample.UncertainRayCount / (Sample.UncertainRayCount + Sample.AccumulatedSampleCount)
					: -1.0,
				Sample.UncertainRayCount + Sample.AccumulatedSampleCount,
				0);
		}
		else
		{
			OutRawSample[0] = float4(
				Sample.LeavingRayCount,
				Sample.AccumulatedSampleCount,
				Sample.UncertainRayCount,
				0);
		}
	}
	else if (SignalBufferLayout == SIGNAL_BUFFER_LAYOUT_PENUMBRA_HISTORY_REJECTION)
	{
		//if (Sample.AccumulatedSampleCount == 0)
		//{
		//	OutRawSample[0].r = 1.0;
		//}
		//else
		//{
		//	OutRawSample[0].r = (254.0 / 255.0) * (Sample.LeavingRayCount / Sample.AccumulatedSampleCount);
		//}

		OutRawSample[0].r = Sample.AccumulatedSampleCount == 0 ? -1 : Sample.LeavingRayCount / Sample.AccumulatedSampleCount;
	}
	else if (SignalBufferLayout == SIGNAL_BUFFER_LAYOUT_REFLECTIONS_HISTORY)
	{
		// Outputs scene color to be compatible with a SSR output.
		OutRawSample[0] = Sample.SceneColor * (Sample.AccumulatedSampleCount > 0 ? rcp(Sample.AccumulatedSampleCount) : 0);
		OutRawSample[1].r = Sample.AccumulatedSampleCount;
		OutBufferCount = 2;
	}
	else if (SignalBufferLayout == SIGNAL_BUFFER_LAYOUT_AO_HISTORY)
	{
		// Outputs number of ray miss in red to be compatible as a SSAO output.
		OutRawSample[0].r = Sample.AccumulatedSampleCount > 0 ? Sample.LeavingRayCount / Sample.AccumulatedSampleCount : 1.0;
		OutRawSample[0].g = Sample.AccumulatedSampleCount;
	}
}

/** Sample input signal. */
FSSFSignalSample SampleInputSignal(Texture2D SignalBuffer0, Texture2D SignalBuffer1, SamplerState Sampler, const uint SignalBufferLayout, float2 UV, float Level = 0)
{
	float4 RawSample[2];
	RawSample[0] = SignalBuffer0.SampleLevel(Sampler, UV, Level);
	RawSample[1] = SignalBuffer1.SampleLevel(Sampler, UV, Level);
	return DecodeInputSignal(SignalBufferLayout, RawSample);
}

/** Sample output signal. */
void OutputSignal(RWTexture2D<float4> OutputSignalBuffer0, RWTexture2D<float4> OutputSignalBuffer1, const uint SignalBufferLayout, const uint2 PixelPosition, FSSFSignalSample Sample)
{
	float4 RawSample[2];
	uint BufferCount;
	EncodeOutputSignal(SignalBufferLayout, Sample, RawSample, BufferCount);

	if (BufferCount >= 1)
		OutputSignalBuffer0[PixelPosition] = RawSample[0];
	if (BufferCount >= 2)
		OutputSignalBuffer1[PixelPosition] = RawSample[1];
}


//------------------------------------------------------- SIGNAL OPERATORS

FSSFSignalSample Mul(FSSFSignalSample Sample, float Scalar)
{
	FSSFSignalSample OutSample;
	OutSample.SceneColor             = Sample.SceneColor * Scalar;
	OutSample.AccumulatedSampleCount = Sample.AccumulatedSampleCount * Scalar;
	OutSample.LeavingRayCount        = Sample.LeavingRayCount * Scalar;
	OutSample.PenumbraClosestHit     = Sample.PenumbraClosestHit * Scalar;
	OutSample.UncertainRayCount      = Sample.UncertainRayCount * Scalar;
	return OutSample;
}

FSSFSignalSample Add(FSSFSignalSample SampleA, FSSFSignalSample SampleB)
{
	SampleA.SceneColor             += SampleB.SceneColor;
	SampleA.AccumulatedSampleCount += SampleB.AccumulatedSampleCount;
	SampleA.LeavingRayCount        += SampleB.LeavingRayCount;
	SampleA.PenumbraClosestHit     += SampleB.PenumbraClosestHit;
	SampleA.UncertainRayCount      += SampleB.UncertainRayCount;
	return SampleA;
}

FSSFSignalSample Minus(FSSFSignalSample Sample)
{
	Sample.SceneColor             = -Sample.SceneColor;
	Sample.AccumulatedSampleCount = -Sample.AccumulatedSampleCount;
	Sample.LeavingRayCount        = -Sample.LeavingRayCount;
	Sample.PenumbraClosestHit     = -Sample.PenumbraClosestHit;
	Sample.UncertainRayCount      = -Sample.UncertainRayCount;
	return Sample;
}

FSSFSignalSample Subtract(FSSFSignalSample SampleA, FSSFSignalSample SampleB)
{
	return Add(SampleA, Minus(SampleB));
}

FSSFSignalSample Abs(FSSFSignalSample Sample)
{
	Sample.SceneColor             = abs(Sample.SceneColor);
	Sample.AccumulatedSampleCount = abs(Sample.AccumulatedSampleCount);
	Sample.LeavingRayCount        = abs(Sample.LeavingRayCount);
	Sample.PenumbraClosestHit     = abs(Sample.PenumbraClosestHit);
	Sample.UncertainRayCount      = abs(Sample.UncertainRayCount);
	return Sample;
}

FSSFSignalSample Sqrt(FSSFSignalSample Sample)
{
	Sample.SceneColor             = sqrt(Sample.SceneColor);
	Sample.AccumulatedSampleCount = sqrt(Sample.AccumulatedSampleCount);
	Sample.LeavingRayCount        = sqrt(Sample.LeavingRayCount);
	Sample.PenumbraClosestHit     = sqrt(Sample.PenumbraClosestHit);
	Sample.UncertainRayCount      = sqrt(Sample.UncertainRayCount);
	return Sample;
}

FSSFSignalSample Power(FSSFSignalSample Sample, float Exponent)
{
	Sample.SceneColor.r           = pow(Sample.SceneColor.r, Exponent);
	Sample.SceneColor.g           = pow(Sample.SceneColor.g, Exponent);
	Sample.SceneColor.b           = pow(Sample.SceneColor.b, Exponent);
	Sample.SceneColor.a           = pow(Sample.SceneColor.a, Exponent);
	Sample.AccumulatedSampleCount = pow(Sample.AccumulatedSampleCount, Exponent);
	Sample.LeavingRayCount        = pow(Sample.LeavingRayCount, Exponent);
	Sample.PenumbraClosestHit     = pow(Sample.PenumbraClosestHit, Exponent);
	Sample.UncertainRayCount      = pow(Sample.UncertainRayCount, Exponent);
	return Sample;
}

FSSFSignalSample Min(FSSFSignalSample SampleA, FSSFSignalSample SampleB)
{
	FSSFSignalSample OutSample;
	OutSample.SceneColor = min(SampleA.SceneColor, SampleB.SceneColor);
	OutSample.AccumulatedSampleCount = min(SampleA.AccumulatedSampleCount, SampleB.AccumulatedSampleCount);
	OutSample.LeavingRayCount = min(SampleA.LeavingRayCount, SampleB.LeavingRayCount);
	OutSample.PenumbraClosestHit = min(SampleA.PenumbraClosestHit, SampleB.PenumbraClosestHit);
	OutSample.UncertainRayCount = min(SampleA.UncertainRayCount, SampleB.UncertainRayCount);
	return OutSample;
}

FSSFSignalSample Max(FSSFSignalSample SampleA, FSSFSignalSample SampleB)
{
	FSSFSignalSample OutSample;
	OutSample.SceneColor = max(SampleA.SceneColor, SampleB.SceneColor);
	OutSample.AccumulatedSampleCount = max(SampleA.AccumulatedSampleCount, SampleB.AccumulatedSampleCount);
	OutSample.LeavingRayCount = max(SampleA.LeavingRayCount, SampleB.LeavingRayCount);
	OutSample.PenumbraClosestHit = max(SampleA.PenumbraClosestHit, SampleB.PenumbraClosestHit);
	OutSample.UncertainRayCount = max(SampleA.UncertainRayCount, SampleB.UncertainRayCount);
	return OutSample;
}

FSSFSignalSample Clamp(FSSFSignalSample Sample, FSSFSignalSample SampleMin, FSSFSignalSample SampleMax)
{
	FSSFSignalSample OutSample;
	OutSample.SceneColor = clamp(Sample.SceneColor, SampleMin.SceneColor, SampleMax.SceneColor);
	OutSample.AccumulatedSampleCount = clamp(Sample.AccumulatedSampleCount, SampleMin.AccumulatedSampleCount, SampleMax.AccumulatedSampleCount);
	OutSample.LeavingRayCount = clamp(Sample.LeavingRayCount, SampleMin.LeavingRayCount, SampleMax.LeavingRayCount);
	OutSample.PenumbraClosestHit = clamp(Sample.PenumbraClosestHit, SampleMin.PenumbraClosestHit, SampleMax.PenumbraClosestHit);
	OutSample.UncertainRayCount = clamp(Sample.UncertainRayCount, SampleMin.UncertainRayCount, SampleMax.UncertainRayCount);
	return OutSample;
}

FSSFSignalSample Lerp(FSSFSignalSample Sample0, FSSFSignalSample Sample1, float Interp)
{
	return Add(Sample0, Mul(Subtract(Sample1, Sample0), Interp));
}

/** Normalize the signal sample to one. */
FSSFSignalSample NormalizeToOneSample(FSSFSignalSample Sample)
{
	FSSFSignalSample OutSample = Mul(Sample, Sample.AccumulatedSampleCount > 0 ? rcp(Sample.AccumulatedSampleCount) : 0);
	OutSample.AccumulatedSampleCount = 1;
	return OutSample;
}
